plugins {
	id 'java-library'
}

apply plugin: 'application'

applicationDefaultJvmArgs = ["-Xms512m", "-Xmx2048m"]

mainClassName = "com.liferay.referenceschecker.main.ReferencesChecker"

executableDir = ""

distributions {
	main {
		contents {
			from("resources/config") {
				include "*.properties"
				into "config"
			}
		}
	}
}

startScripts {
	applicationName = 'references-checker'
	executableDir = '.'
	doLast {
		// By default, scripts are included in bin, changing it to root folder
		unixScript.text = unixScript.text.replace('\\\"\$PRG\\\"`/..', '\\\"\$PRG\\\"`')
		windowsScript.text = windowsScript.text.replace('APP_HOME=%DIRNAME%..', 'APP_HOME=%DIRNAME%')
		// Add config folder to classpath
		unixScript.text = unixScript.text.replace('CLASSPATH=\$APP_HOME/', 'CLASSPATH=\$APP_HOME/config:\$APP_HOME/')
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=%APP_HOME%\\', 'set CLASSPATH=%APP_HOME%\\config;%APP_HOME%\\')

	}
}

dependencies {
	compile group: "com.beust", name: "jcommander", version: "1.72"
	compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.7"
	compile group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-csv", version: "2.9.7"
	compile group: "com.zaxxer", name: "HikariCP", version: "3.2.0"
	compile group: "jline", name: "jline", version: "2.14.6"
	compile group: "log4j", name: "log4j", version: "1.2.17"
	compile group: "mysql", name: "mysql-connector-java", version: "5.1.47"
	compile group: "org.apache.commons", name: "commons-lang3", version: "3.8"
	compile group: "org.apache.commons", name: "commons-text", version: "1.3"
	compile group: "org.mariadb.jdbc", name: "mariadb-java-client", version: "2.3.0"
	compile group: "org.postgresql", name: "postgresql", version: "42.2.5"
	compile group: "org.slf4j", name: "slf4j-simple", version: "1.7.25"
	compile project(":references-checker")
}