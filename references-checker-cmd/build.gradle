plugins {
	id 'java-library'
}

apply plugin: 'application'

applicationDefaultJvmArgs = ["-Xms512m", "-Xmx2048m"]

mainClassName = "com.liferay.referencechecker.main.CheckDatabase"

executableDir = ""

dependencies {
	compile group: "com.beust", name: "jcommander", version: "1.72"
	compile group: "com.microsoft.sqlserver", name: "mssql-jdbc", version: "6.4.0.jre8"
	compile group: "com.oracle.jdbc", name: "ojdbc8", version: "18.3.0.0"
	compile group: "com.zaxxer", name: "HikariCP", version: "3.2.0"
	compile group: "commons-io", name: "commons-io", version: "2.6"
	compile group: "jline", name: "jline", version: "2.14.6"
	compile group: "log4j", name: "log4j", version: "1.2.17"
	compile group: "mysql", name: "mysql-connector-java", version: "5.1.47"
	compile group: "org.apache.commons", name: "commons-lang3", version: "3.8.1"
	compile group: "org.apache.commons", name: "commons-text", version: "1.3"
	compile group: "org.mariadb.jdbc", name: "mariadb-java-client", version: "2.3.0"
	compile group: "org.postgresql", name: "postgresql", version: "42.2.5"
	compile group: "org.slf4j", name: "slf4j-simple", version: "1.7.25"
	compile project(":references-checker")
}

distributions {
	main {
		contents {
			from("resources/config") {
				include "*.properties"
				into "config"
			}
		}
	}
}

startScripts {
	applicationName = 'check-database'
	executableDir = '.'
	doLast {
		// By default, scripts are included in bin, changing it to root folder
		unixScript.text = unixScript.text.replace('\\\"\$PRG\\\"`/..', '\\\"\$PRG\\\"`')
		windowsScript.text = windowsScript.text.replace('APP_HOME=%DIRNAME%..', 'APP_HOME=%DIRNAME%')
		// Add config folder to classpath
		unixScript.text = unixScript.text.replace('CLASSPATH=\$APP_HOME/', 'CLASSPATH=\$APP_HOME/config:\$APP_HOME/')
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=%APP_HOME%\\', 'set CLASSPATH=%APP_HOME%\\config;%APP_HOME%\\')

	}
}

task databaseInfoExtraRunApp(type: CreateStartScripts)
task dumpReferencesExtraRunApp(type: CreateStartScripts)

databaseInfoExtraRunApp {
	defaultJvmOpts = ["-Xms512m", "-Xmx2048m"]

	mainClassName = "com.liferay.referencechecker.main.DatabaseInfo"
	applicationName = 'database-info'

	classpath = startScripts.classpath
	outputDir = startScripts.outputDir

	databaseInfoExtraRunApp {
		doLast {
			// By default, scripts are included in bin, changing it to root folder
			unixScript.text = unixScript.text.replace('\\\"\$PRG\\\"`/..', '\\\"\$PRG\\\"`')
			windowsScript.text = windowsScript.text.replace('APP_HOME=%DIRNAME%..', 'APP_HOME=%DIRNAME%')
			// Add config folder to classpath
			unixScript.text = unixScript.text.replace('CLASSPATH=\$APP_HOME/', 'CLASSPATH=\$APP_HOME/config:\$APP_HOME/')
			windowsScript.text = windowsScript.text.replace('set CLASSPATH=%APP_HOME%\\', 'set CLASSPATH=%APP_HOME%\\config;%APP_HOME%\\')

		}
	}
}

dumpReferencesExtraRunApp {
	defaultJvmOpts = ["-Xms512m", "-Xmx2048m"]

	mainClassName = "com.liferay.referencechecker.main.DumpReferences"
	applicationName = 'dump-references'

	classpath = startScripts.classpath
	outputDir = startScripts.outputDir

	dumpReferencesExtraRunApp {
		doLast {
			// By default, scripts are included in bin, changing it to root folder
			unixScript.text = unixScript.text.replace('\\\"\$PRG\\\"`/..', '\\\"\$PRG\\\"`')
			windowsScript.text = windowsScript.text.replace('APP_HOME=%DIRNAME%..', 'APP_HOME=%DIRNAME%')
			// Add config folder to classpath
			unixScript.text = unixScript.text.replace('CLASSPATH=\$APP_HOME/', 'CLASSPATH=\$APP_HOME/config:\$APP_HOME/')
			windowsScript.text = windowsScript.text.replace('set CLASSPATH=%APP_HOME%\\', 'set CLASSPATH=%APP_HOME%\\config;%APP_HOME%\\')

		}
	}
}

applicationDistribution.into('.') {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from(databaseInfoExtraRunApp)
	from(dumpReferencesExtraRunApp)
	fileMode = 0755
}