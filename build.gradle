plugins {
	id "com.liferay.source.formatter" version "2.3.237" apply false
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'

	jar {
		manifest {
			attributes(
				'Built-By'              : System.properties['user.name'],
				'Build-Timestamp'       : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Created-By'            : "Gradle ${gradle.gradleVersion}",
				'Build-Jdk'             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				'Build-OS'              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
				'Implementation-Title'  : "Reference Checher: ${baseName} jar",
				'Implementation-Version': "${project.rootProject.version}",
				'Implementation-Vendor' : "Liferay"
			)
		}
	}

	sourceCompatibility = "1.7"
	targetCompatibility = "1.7"

	repositories {
		mavenCentral()
		maven {
			url "https://www.oracle.com/content/secure/maven/content"
			name "maven.oracle.com"
			credentials {
				username mavenOracleUsername
				password mavenOraclePassword
			}
		}
	}

	buildDir = new File(rootProject.projectDir, "build/" + project.name)

	task copyJars(type: Copy) {
		from subprojects.collect { it.tasks.withType(Jar) }
		into new File(rootProject.projectDir, "lib")
	}

	task copyDist(type: Copy) {
		dependsOn ':references-checker-cmd:distZip'
		from new File(rootProject.projectDir, "build/references-checker-cmd/distributions")
		into new File(rootProject.projectDir, "dist")
	}

	task deploy {
		group 'Distribution'
		description 'Create all distributable files (dist and lib folders)'
		dependsOn 'copyJars'
		dependsOn 'copyDist'
	}

subprojects {
	apply plugin: 'com.liferay.source.formatter'

	check {
		dependsOn checkSourceFormatting
	}
}

	version = referenceCheckerVersion
}